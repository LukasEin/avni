      SUBROUTINE DPDAZ(EPLA,EPLO,AZIM,DELTA,XLAT,XLON)
Cf2py intent(inout) EPLA,EPLO,AZIM,DELTA
Cf2py intent(out) XLAT,XLON
C
C     INPUT PARAMETERS:
C        EPLA : EPICENTRAL LATITUDE
C        EPLO : EPICENTRAL LONGITUDE
C        AZIM : AZIMUTH OF RECEIVER AS SEEN FROM EPICENTER
C        DELTA: ANGULAR DISTANCE
C     OUTPUT PARAMETERS:
C        XLAT : RECEIVER LATITUDE
C        XLON : RECEIVER LONGITUDE
C
C     AZIMUTH IS MEASURED COUNTERCLOCKWISE FROM THE NORTH
C     ALL ANGLES ARE IN DEGREES
C
C       [HOFFMAN TRADITIONAL ROUTINE]
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (RADIAN=180.D0/3.1415926535D0)
C      DATA RADIAN/57.29578/
      PH21=(180.D0-AZIM)/RADIAN
      TH1=(90.D0-EPLA)/RADIAN
      PH1=EPLO/RADIAN
      DEL=DELTA/RADIAN
      STH1=DSIN(TH1)
      CTH1=DCOS(TH1)
      SPH1=DSIN(PH1)
      CPH1=DCOS(PH1)
      SDEL=DSIN(DEL)
      CDEL=DCOS(DEL)
      CPH21=DCOS(PH21)
      SPH21=DSIN(PH21)
      CTH2=-SDEL*CPH21*STH1+CDEL*CTH1
      STH2=DSQRT(1.D0-CTH2*CTH2)
      CPH2=(SDEL*(CPH21*CTH1*CPH1-SPH21*SPH1)+CDEL*STH1*CPH1)
      SPH2=(SDEL*(CPH21*CTH1*SPH1+SPH21*CPH1)+CDEL*STH1*SPH1)
      TH2=DATAN2(STH2,CTH2)
      PH2=DATAN2(SPH2,CPH2)
      XLAT=90.D0-TH2*RADIAN
      XLON=PH2*RADIAN
      RETURN
      END
